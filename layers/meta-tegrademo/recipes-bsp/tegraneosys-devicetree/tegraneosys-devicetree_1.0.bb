DESCRIPTION = "Meta-tegrademo example device trees for out-of-tree builds."
HOMEPAGE = "https://github.com/OE4T"
LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302"

inherit devicetree

COMPATIBLE_MACHINE = "neosys-nvr50p"

S = "${WORKDIR}/sources"
UNPACKDIR = "${S}"

SRC_URI = "\
    file://nru50p-onx16.dtb \
    file://nru50p-onx8.dtb  \
    file://nru50p-onano4.dtb \
"

KERNEL_INCLUDE = " \
    ${STAGING_KERNEL_DIR}/nvidia/soc/tegra/kernel-include \
    ${STAGING_KERNEL_DIR}/nvidia/platform/tegra/common/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/soc/t19x/kernel-include \
    ${STAGING_KERNEL_DIR}/nvidia/soc/t19x/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/platform/t19x/common/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/soc/t23x/kernel-include \
    ${STAGING_KERNEL_DIR}/nvidia/soc/t23x/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/platform/t23x/common/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/platform/t19x/galen/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/platform/t19x/jakku/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/platform/t19x/mccoy/kernel-dts \
    ${STAGING_KERNEL_DIR}/nvidia/platform/t23x/concord/kernel-dts \
    ${STAGING_KERNEL_DIR}/scripts/dtc/include-prefixes \
"

# Straight from arch/arm64/boot/dts in kernel source tree
DTC_PPFLAGS:append = " -DLINUX_VERSION=504 -DTEGRA_HOST1X_DT_VERSION=1"

# re-implement function from devicetree.bbclass to preserve order of KERNEL_INCLUDE
def expand_includes(varname, d):
    import glob
    includes = list()
    # expand all includes with glob
    for i in (d.getVar(varname) or "").split():
        for g in glob.glob(i):
            if os.path.isdir(g): # only add directories to include path
                includes.append(g)
    return includes
